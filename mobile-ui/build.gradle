apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        multiDexEnabled = true
        testInstrumentationRunner "org.buffer.android.boilerplate.ui.TestRunner"
    }

    dexOptions {
        preDexLibraries = false
        dexInProcess = false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

}

kapt {
    generateStubs = true
    correctErrorTypes = true
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

dependencies {

    final ESPRESSO_VERSION = '2.2.2'
    final RUNNER_VERSION = '0.5'

    def mobileUiDependencies = rootProject.ext.mobileUiDependencies
    def mobileUiTestDependencies = rootProject.ext.mobileUiTestDependencies

    compile project(':presentation')
    compile project(':data')
    compile project(':cache')
    compile project(':remote')

    compile mobileUiDependencies.javaxAnnotation

    compile mobileUiDependencies.kotlin
    compile mobileUiDependencies.javaxInject
    compile mobileUiDependencies.rxKotlin
    compile mobileUiDependencies.androidAnnotations
    compile mobileUiDependencies.androidSupportV4
    compile mobileUiDependencies.androidSupportV13
    compile mobileUiDependencies.appCompatV7
    compile mobileUiDependencies.supportRecyclerView
    compile mobileUiDependencies.supportDesign
    compile mobileUiDependencies.timber

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.bumptech.glide:glide:4.0.0'

    // Instrumentation test dependencies
    androidTestCompile mobileUiTestDependencies.junit
    androidTestCompile mobileUiTestDependencies.mockito
    androidTestCompile ("com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ("com.android.support.test:runner:$RUNNER_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ("com.android.support.test:rules:$RUNNER_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ("com.android.support.test.espresso:espresso-intents:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'
    final DAGGER_VERSION = '2.11'

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile  "com.google.dagger:dagger:$DAGGER_VERSION"
    compile  "com.google.dagger:dagger-android-support:$DAGGER_VERSION"

    kapt daggerCompiler
    kapt  "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    kaptTest daggerCompiler
    kaptAndroidTest daggerCompiler
}